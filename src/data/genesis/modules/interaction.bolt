####################################################################################
## Util for calling functions on interaction entities when clicked
## Tag entities with `genesis.interaction` to have them checked.
## Use `call_on_lclick()` and `call_on_rclick()` inside of a mcfunction
## definition to have that function called when a tagged interaction is clicked.
## Use `call_on_lclick_tagged(my_tag)` and `call_on_rclick_tagged(my_tag)` inside of a mcfunction
## definition to have that function called when a tagged interaction is clicked, if
## it also has an additional tag `my_tag` to distinguish it from other interactions.
## Example:
##
## from genesis:interaction import call_on_lclick_tagged
##
## function ~/clicked_cooking_pot:
##   call_on_lclick_tagged("genesis.cooking_pot")
##   say hi
####################################################################################

def call_on_lclick():
  append function_tag genesis:interaction/lclicked {
    "values": [ (~/) ]
  }
def call_on_rclick():
  append function_tag genesis:interaction/rclicked {
    "values": [ (~/) ]
  }
def call_on_lclick_tagged(tag_name):
  HERE = ~/
  append function genesis:interaction/internal/lclick_tagged:
    execute if entity @s[tag=tag_name] run function HERE
def call_on_rclick_tagged(tag_name):
  HERE = ~/
  append function genesis:interaction/internal/rclick_tagged:
    execute if entity @s[tag=tag_name] run function HERE


function ~/internal:
  function ~/lclick_tagged:
    call_on_lclick()
  function ~/rclick_tagged:
    call_on_rclick()
  path = ~/lclick
  advancement path {
    "criteria": {
      "interact": {
        "trigger": "minecraft:player_hurt_entity",
        "conditions": {
          "entity": {
            "type": "minecraft:interaction",
            "nbt": "{Tags:[\"genesis.interaction\"]}"
          }
        }
      }
    },
    "requirements": [
      [
        "interact"
      ]
    ],
    "rewards": {
      "function": path
    }
  }
  predicate path {
    "condition": "entity_properties",
    "entity": "this",
    "predicate": {
      "nbt": "{attack:{}}"
    }
  }
  function path:
    advancement revoke @s only path
    tag @s add genesis.interaction.player
    execute at @s anchored eyes positioned ^ ^ ^4 run function ~/search:
      data modify storage genesis:temp interaction.player_UUID set from entity @s UUID
      scoreboard players set #interaction.responder genesis 0

      tag @e[type=interaction,tag=genesis.interaction.responder,distance=..8] remove genesis.interaction.responder
      execute as @e[type=interaction,tag=genesis.interaction,predicate=path,distance=..8] at @s run function ~/verify:
        data modify storage genesis:temp temp set from storage genesis:temp interaction.player_UUID
        execute store success score #different genesis run data modify storage genesis:temp temp set from entity @s attack.player
        execute if score #different genesis matches 0 run scoreboard players set #interaction.verified genesis 1

        execute if score #interaction.verified genesis matches 1 if score #interaction.responder genesis matches 0 run tag @s add genesis.interaction.responder
        execute if score #interaction.verified genesis matches 1 run scoreboard players set #interaction.responder genesis 1

      execute if score #interaction.responder genesis matches 0 run return 0

      execute as @e[type=interaction,tag=genesis.interaction.responder,distance=..8,limit=1] at @s run function ~/get:
        function #genesis:interaction/lclicked
        tag @s remove genesis.interaction.responder
        data remove entity @s attack
    tag @s remove genesis.interaction.player


  path = ~/rclick
  advancement path {
    "criteria": {
      "interact": {
        "trigger": "minecraft:player_interacted_with_entity",
        "conditions": {
          "entity": {
            "type": "minecraft:interaction",
            "nbt": "{Tags:[\"genesis.interaction\"]}"
          }
        }
      }
    },
    "requirements": [
      [
        "interact"
      ]
    ],
    "rewards": {
      "function": path
    }
  }
  predicate path {
    "condition": "entity_properties",
    "entity": "this",
    "predicate": {
      "nbt": "{interaction:{}}"
    }
  }
  function path:
    advancement revoke @s only path
    tag @s add genesis.interaction.player
    execute at @s anchored eyes positioned ^ ^ ^4 run function ~/search:
      data modify storage genesis:temp interaction.player_UUID set from entity @s UUID
      scoreboard players set #interaction.responder genesis 0

      tag @e[type=interaction,tag=genesis.interaction.responder,distance=..8] remove genesis.interaction.responder
      execute as @e[type=interaction,tag=genesis.interaction,predicate=path,distance=..8] at @s run function ~/verify:
        data modify storage genesis:temp temp set from storage genesis:temp interaction.player_UUID
        execute store success score #different genesis run data modify storage genesis:temp temp set from entity @s interaction.player
        execute if score #different genesis matches 0 run scoreboard players set #interaction.verified genesis 1

        execute if score #interaction.verified genesis matches 1 if score #interaction.responder genesis matches 0 run tag @s add genesis.interaction.responder
        execute if score #interaction.verified genesis matches 1 run scoreboard players set #interaction.responder genesis 1

      execute if score #interaction.responder genesis matches 0 run return 0

      execute as @e[type=interaction,tag=genesis.interaction.responder,distance=..8,limit=1] at @s run function ~/get:
        function #genesis:interaction/rclicked
        tag @s remove genesis.interaction.responder
        data remove entity @s interaction
    tag @s remove genesis.interaction.player
      
    